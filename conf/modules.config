/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Config file for defining DSL2 per module options and publishing paths
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Available keys to override module options:
        ext.args   = Additional arguments appended to command in module.
        ext.args2  = Second set of arguments appended to command in module (multi-tool modules).
        ext.args3  = Third set of arguments appended to command in module (multi-tool modules).
        ext.prefix = File name prefix for output files.
----------------------------------------------------------------------------------------
*/

process {

    publishDir = [
        path: { "${params.outdir}/${task.process.tokenize(':')[-1].tokenize('_')[0].toLowerCase()}" },
        mode: params.publish_dir_mode,
        saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
    ]
    withName: 'MULTIQC' {
        ext.args   = { params.multiqc_title ? "--title \"$params.multiqc_title\"" : '' }
        publishDir = [
            path: { "${params.outdir}/${params.variant_type}/multiqc" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // subsample_vcf test

    withName: BCFTOOLS_SORT {
        ext.prefix = { vcf.baseName - ".vcf" + ".sort"}
        ext.args   = {"--output-type z --write-index=tbi" }
        publishDir = [
            enabled: false
        ]
    }

    withName: BCFTOOLS_VIEW_SUBSAMPLE {
        ext.prefix = {  vcf.baseName - ".vcf" + ".subsample" }
        ext.args  = {"--output-type z -s ${meta.subsample}" }
        publishDir = [
            enabled: false
        ]
    }

    withName: BCFTOOLS_VIEW_FILTERMISSING {
        ext.prefix = {  vcf.baseName - ".vcf" + ".filtermissing" }
        ext.args  = {"--output-type z -e 'AC=0'" }
        publishDir = [
            enabled: false
        ]
    }

    // sv_vcf_conversions

    withName: VARIANT_EXTRACTOR {
        ext.prefix = { input.baseName - ".vcf" + ".variantextract" }
        publishDir = [
            enabled: false
        ]
    }

    withName: SVYNC {
        ext.prefix = {vcf.baseName - ".vcf" + ".svync"}
        publishDir = [
            path: { "${params.outdir}/test" },
            enabled: false
        ]
    }

    withName: BGZIP_TABIX {
        publishDir = [
            enabled: false
        ]
    }
    //// prepare_vcfs ////

    // liftover_vcfs

    withName: PICARD_CREATESEQUENCEDICTIONARY {
        publishDir = [
            path: {"${params.outdir}/references/dictionary"},
            pattern: "*{.dict}",
            mode: params.publish_dir_mode
        ]
    }

    withName: PICARD_LIFTOVERVCF {
        ext.prefix = {input_vcf.baseName - ".vcf"}
        ext.args = {"--WARN_ON_MISSING_CONTIG true"}
        publishDir = [
            enabled: false
        ]
    }
    withName: BCFTOOLS_RENAME_CHR {
        ext.prefix = {input.baseName - ".vcf" + ".renamechr"}
        ext.args   = {"--output-type z"}
        publishDir = [
            path: {params.liftover == "truth"? "${params.outdir}/${params.variant_type}/${params.truth_id}/preprocess/liftover" : "${params.outdir}/${params.variant_type}/${meta.id}/preprocess/liftover"},
            pattern: "*{.vcf.gz}",
            mode: params.publish_dir_mode
        ]
    }
    withName: UCSC_LIFTOVER {
        ext.prefix = {bed.baseName - ".bed"}
        publishDir = [
            enabled: false
        ]
    }
    withName: SORT_BED {
        ext.prefix = {bed.baseName - ".bed" + "sort"}
        publishDir = [
            enabled: false
        ]
    }
    withName: BEDTOOLS_MERGE {
        ext.prefix = {bed.toString() - ".bed" + ".merged" }
        publishDir = [
            path: {params.liftover == "truth"? "${params.outdir}/${params.variant_type}/${params.truth_id}/preprocess/liftover" : "${params.outdir}/${params.variant_type}/${meta.id}/preporcess/liftover"},
            pattern: "*{.bed}",
            mode: params.publish_dir_mode
        ]
    }
    withName: REFORMAT_HEADER {
        ext.prefix ={["${meta.id}",
                    (meta.tag != null)      ? ".${meta.tag}" : ''
                    ].join('').trim()
                    }
        publishDir = [
            enabled: false
        ]

    }

    withName: "BCFTOOLS_REHEADER*" {
        ext.args2   = {"--output-type z --write-index=tbi" }
        ext.prefix = { vcf.baseName - ".vcf" + ".reheader"}
        publishDir = [
            enabled: false
        ]
    }

    // filtering contigs
    withName: BCFTOOLS_VIEW_CONTIGS {
        ext.prefix = { vcf.baseName - ".vcf.gz" + ".filtercontigs"  }
        ext.args  = {[
                "--output-type z --write-index=tbi",
                (params.genome.contains("38"))? "-r chr1,chr2,chr3,chr4,chr5,chr6,chr7,chr8,chr9,chr10,chr11,chr12,chr13,chr14,chr15,chr16,chr17,chr18,chr19,chr20,chr21,chr22,chrX,chrY" : "-r 1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,X,Y"
        ].join(' ').trim() }
        publishDir = [
            enabled: false
        ]
    }

    // bcftools normalize
    withName: BCFTOOLS_NORM {
        ext.prefix = { vcf.baseName - ".vcf" + ".norm"}
        ext.args   = {"--output-type z -c w --write-index=tbi" }
        publishDir = [
            enabled: false
        ]
    }

    //bcftools split multi allelics
    withName: BCFTOOLS_SPLIT_MULTI {
        ext.prefix = { vcf.baseName - ".vcf" + ".split"}
        ext.args   = {"--output-type z -m-any -c w --write-index=tbi" }
        publishDir = [
            enabled: false
        ]
    }

    // bcftools deduplicate variants
    withName: BCFTOOLS_DEDUP {
        ext.prefix = { vcf.baseName - ".vcf" + ".dedup"}
        ext.args   = {"--output-type z --rm-du exact -c w" }
        publishDir = [
            enabled: false
        ]
    }

    // filtering tools
    withName: BCFTOOLS_FILTER {
        ext.prefix = { vcf.baseName - ".vcf" + ".filter"}
        ext.args  = {[
                "--output-type v",
                (params.include_expression != null )? "--include '$params.include_expression'" : "",
                (params.exclude_expression != null )? "--exclude '$params.exclude_expression'" : ""
        ].join(' ').trim() }
        publishDir = [
            enabled: false
        ]
    }

    withName: TABIX_TABIX {
        publishDir = [
            enabled: false
        ]
    }

    withName: SURVIVOR_FILTER {
        ext.prefix = { vcf_file.baseName - ".vcf" + ".filter"}
        publishDir = [
            enabled: false
        ]
    }

    // split_small_variants_test

    withName: BCFTOOLS_VIEW_SNV {
        ext.prefix = { vcf.baseName - ".vcf" + ".snv" }
        ext.args  = {"--output-type v --types snps" }
        publishDir = [
            enabled: false
        ]
    }

    withName: BCFTOOLS_VIEW_INDEL {
        ext.prefix = { vcf.baseName - ".vcf" + ".indel" }
        ext.args  = {"--output-type v --types indels" }
        publishDir = [
            enabled: false
        ]
    }

    withName: 'PUBLISH_PROCESSED_VCF' {
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/${meta.id}/preprocess"},
            pattern: "*{.vcf.gz,vcf.gz.tbi}",
            mode: params.publish_dir_mode
        ]
    }

    // report_vcf_statistics

    withName: SURVIVOR_STATS {
        ext.prefix ={["${meta.id}",
                    (meta.caller != null)      ? ".${meta.caller}_mqc" : '_mqc'
                    ].join('').trim()
                    }
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/${meta.id}/stats/survivor/"},
            pattern: "*{.stats}",
            mode: params.publish_dir_mode
        ]
    }

    withName: BCFTOOLS_STATS {
        ext.prefix ={["${meta.id}",
                    (meta.caller != null)      ? ".${meta.caller}" : ''
                    ].join('').trim()
                    }
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/${meta.id}/stats/bcftools/"},
            pattern: "*{stats.txt}",
            mode: params.publish_dir_mode
        ]
    }

    //// benchmarking //

    withName: "RTGTOOLS_FORMAT" {
        publishDir = [
            path: {"${params.outdir}/references/rtgtools"},
            pattern: "*{.sdf}",
            mode: params.publish_dir_mode
        ]
    }

    withName: "RTGTOOLS_VCFEVAL" {
        ext.prefix = {"${meta.id}.${params.truth_id}.${meta.caller}"}
        ext.args   = {"--all-record"}
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/${meta.id}/benchmarks/rtgtools"},
            pattern: "*{.vcf.gz,vcf.gz.tbi,tsv.gz,txt}",
            mode: params.publish_dir_mode
        ]
    }
    // squash-ploidy is necessary to be able to match het-hom changes
    withName: "RTGTOOLS_VCFEVAL_SOMATIC" {
        ext.prefix = {"${meta.id}.${params.truth_id}.${meta.caller}"}
        ext.args   = {"--all-record --squash-ploidy"}
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/${meta.id}/benchmarks/rtgtools"},
            pattern: "*{.vcf.gz,vcf.gz.tbi,tsv.gz,txt}",
            mode: params.publish_dir_mode
        ]
    }

    withName: "HAPPY_HAPPY" {
        ext.prefix = {"${meta.id}.${params.truth_id}.${meta.caller}"}
        //ext.args   = {""}
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/${meta.id}/benchmarks/happy"},
            pattern: "*{.csv.gz,csv,json.gz,json,vcf.gz,vcf.gz.tbi}",
            mode: params.publish_dir_mode
        ]
    }

    withName: "HAPPY_SOMPY" {
        ext.prefix = {"${meta.id}.${params.truth_id}.${meta.caller}"}
        ext.args = { meta.caller.contains("strelka") || meta.caller.contains("varscan") || meta.caller.contains("pisces") ||  meta.caller == "mutect" ? "--feature-table hcc.${meta.caller}.${params.variant_type} --bin-afs" : "--feature-table generic" }
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/${meta.id}/benchmarks/sompy"},
            pattern: "*{.csv.gz,csv,json.gz,json,vcf.gz,vcf.gz.tbi,csv}",
            mode: params.publish_dir_mode
        ]
    }

    withName: "SPLIT_SOMPY_FEATURES" {
        publishDir = [
            enabled: false
        ]
    }

    withName: "HAPPY_PREPY" {
        ext.prefix = {"${meta.id}.${params.truth_id}.${meta.caller}.prepy"}
        ext.args   = {"--fixchr --filter-nonref --bcftools-norm"}
        publishDir = [
            enabled: false
        ]
    }

    withName: "TRUVARI_BENCH" {
        ext.prefix = {"${meta.id}.${params.truth_id}.${meta.caller}"}
        ext.args = {[
                "--sizemin 0 --sizefilt 0 --sizemax 5000000",
                (meta.pctsize != null)      ? " --pctsize ${meta.pctsize}" : '',
                (meta.pctovl != null)       ? " --pctovl ${meta.pctovl}" : '',
                (meta.pctseq != null)       ? " --pctseq ${meta.pctseq}" :(params.variant_type == "copynumber") ? ' --pctseq 0': '',
                (meta.refdist != null)      ? " --refdist ${meta.refdist}" : '',
                (meta.chunksize != null)    ? " --chunksize ${meta.chunksize}" : '',
                (meta.dup_to_ins)   ? " --dup-to-ins" : '',
                (meta.typeignore || params.variant_type == "copynumber")   ? " --typeignore" : ''
                ].join('').trim()}
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/${meta.id}/benchmarks/truvari"},
            pattern: "*{.vcf.gz,vcf.gz.tbi,json}",
            mode: params.publish_dir_mode
        ]
    }

    withName: SVANALYZER_SVBENCHMARK {
        ext.prefix = {"${meta.id}.${params.truth_id}.${meta.caller}"}
        ext.args = {[
                (meta.normshift != null)      ? " -normshift ${meta.normshift}" : '',
                (meta.normdist != null)       ? " -normdist ${meta.normdist}" : '',
                (meta.normsizediff != null)   ? " -normsizediff ${meta.normsizediff}" : '',
                (meta.maxdist != null)        ? " -maxdist ${meta.maxdist}" : ''
                ].join('').trim()}
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/${meta.id}/benchmarks/svanalyzer"},
            pattern: "*{.vcf.gz,tbi,distances,log,report}",
            mode: params.publish_dir_mode
        ]
    }

    withName: WITTYER {
        ext.prefix = {"${meta.id}.${params.truth_id}.${meta.caller}"}
        ext.args = {[
                "--includedFilters=''",
                (meta.evaluationmode != null)      ? " -em ${meta.evaluationmode}" : '',
                (meta.percentThreshold != null)    ? " --pt ${meta.percentThreshold}" : '',
                (meta.absoluteThreshold != null)   ? " --at ${meta.absoluteThreshold}" : '',
                (meta.bpDistance != null)          ? " --bpd ${meta.bpDistance}" : '',
                (meta.maxMatches != null)          ? " --mm ${meta.maxMatches}" : ''
                ].join('').trim()}
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/${meta.id}/benchmarks/wittyer"},
            pattern: "*{.vcf.gz,tbi,json}",
            mode: params.publish_dir_mode
        ]
    }
    withName: BEDTOOLS_INTERSECT {
        ext.prefix = {"${meta.id}.${params.truth_id}.${meta.caller}"}
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/${meta.id}/intersect"},
            pattern: "*{.bed,csv}",
            mode: params.publish_dir_mode
        ]
    }

    withName: "TABIX_BGZIP*"{
        ext.prefix = {input.toString() - ".vcf.gz"}
        publishDir = [
            enabled: false
        ]
    }
    withName: "TABIX_BGZIPTABIX*"{
        ext.prefix = { input.baseName - ".vcf" + ".bgzip"}
        publishDir = [
            enabled: false
        ]
    }

    // report_benchmark_statistics

    withName: MERGE_REPORTS {
        ext.prefix = {"${meta.benchmark_tool}"}
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/summary/tables/${meta.benchmark_tool}"},
            pattern: "*{csv}",
            mode: params.publish_dir_mode
        ]
    }

    withName: PLOTS {
        ext.prefix = {"${meta.benchmark_tool}_mqc"}
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/summary/plots/${meta.benchmark_tool}"},
            pattern: "*{png}",
            mode: params.publish_dir_mode
        ]
    }

    withName: CREATE_DATAVZRD_INPUT {
        publishDir = [
            enabled: false
        ]
    }

    withName: DATAVZRD {
        ext.prefix = {"${meta.id}"}
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/summary/datavzrd/"},
            pattern: "*",
            mode: params.publish_dir_mode
        ]
    }

    // compare_benchmark_results

    withName: TABIX_BGZIP_UNZIP{
        ext.prefix = {input.toString() - ".vcf.gz"}
        publishDir = [
            enabled: false
        ]
    }

    withName: SURVIVOR_MERGE {
        ext.prefix = {"${meta.id}.${meta.tag}"}
        publishDir = [
            enabled: false
        ]
    }

    withName: BCFTOOLS_MERGE {
        ext.prefix = {"${meta.id}.${meta.tag}"}
        ext.args   = {"--output-type v --force-samples --force-single"}
        publishDir = [
            enabled: false
        ]
    }

    withName: VCF_TO_CSV {
        ext.prefix = {"${meta.id}.${meta.tag}"}
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/summary/comparisons/${meta.id}"},
            pattern: "*{.csv}",
            mode: params.publish_dir_mode
        ]
    }

    withName: MERGE_SOMPY_FEATURES {
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/summary/comparisons/${meta.id}"},
            pattern: "*{csv}",
            mode: params.publish_dir_mode
        ]
    }

    // VCF2BED tools

    withName: "SVTK_VCF2BED" {
        ext.args   = {"--no-samples"}
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/${meta.id}/intersect"},
            pattern: "*{.bed}",
            mode: params.publish_dir_mode
        ]
    }
    withName: "BEDOPS_CONVERT2BED" {
        publishDir = [
            path: {"${params.outdir}/${params.variant_type}/${meta.id}/intersect"},
            pattern: "*{.bed}",
            mode: params.publish_dir_mode
        ]
    }

}
